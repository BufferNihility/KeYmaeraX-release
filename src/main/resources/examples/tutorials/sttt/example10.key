Functions.
  R A.
  R B.
  R b.
  R S.
  R ep.
  R lw.
  R ly.
  R a().
  R r().
End.

ProgramVariables.
  R x.
  R y.
  R v.
  R a.
  R r.
  R dx.
  R dy.
  R w.
  R c.
End.

Problem.
    v >= 0 & A > 0 & B >= b & b > 0 & ep > 0 & lw > 0 & y = ly & r() != 0 & dx^2 + dy^2 = 1
           & ((y >= ly -> (y-ly) + v^2/(2*B) < lw) | (y <= ly -> (ly-y) + v^2/(2*B) < lw))
 -> [
      { {   ?(y >= ly -> (y-ly) + x+v^2/(2*b) + (A/b+1)*(A/2*ep^2+ep*v) < lw)
            |(y <= ly -> (ly-y) + x+v^2/(2*b) + (A/b+1)*(A/2*ep^2+ep*v) < lw);
            a := *; ?-B <= a & a <= A;
            w := *; r := *; ?r != 0 & w*r = v;
         ++ ?v=0; a := 0; w := 0; r := r
         ++ a := *; w := w; r := r; ?-B <=a & a <= -b
        };

        ?a()=a & r() = r; /* needed because we neither support a() := nor v'=a */
        c := 0;
        x' = v*dx, y' = v*dy, v' = a(), dx' = -dy*w, dy' = dx*w, w'=a()/r(), c' = 1 & v >= 0 & c <= ep;
      }*@invariant(v >= 0 & dx^2+dy^2 = 1 & r() != 0 & ((y >= ly -> (y-ly) + v^2/(2*B) < lw) | (y <= ly -> (ly-y) + v^2/(2*B) < lw)))
    ] ((y >= ly -> (y-ly) < lw) | (y <= ly -> (ly-y) < lw))
End.