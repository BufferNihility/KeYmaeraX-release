/**
 * Created by jeannin on 8/6/2015
 * @author Jean-Baptiste Jeannin
 */

/** Condition with maxz and abs
      (
        \forall t \forall ro \forall ho
        ((0 <= t & t < maxz(w * (dhf - dhd)) / a &
          ro = rv * t & ho = (w * a) / 2 * t^2 + dhd * t) |
         (t >= maxz(w * (dhf - dhd)) / a &
          ro = rv * t & ho = dhf * t - w * (maxz(w * (dhf - dhd)))^2 / (2*a))
         -> (abs(r - ro) > rp | w * h < w * ho - hp))
      )
*/

ProgramVariables.
  /** Variables **/
   /* horizontal */
  R r.    /* relative distance in ft; xi - xo */
          /* x = r */
  R rv.   /* relative speed -(vi - vo) */
   /* vertical */
  R h.    /* relative altitude in ft */
          /* if negative: the intruder is lower than the ownship */
          /* h := hi - ho */
          /* z = h */
  R dhd.  /* vertical velocity of ownship */
  R dhf.  /* target minimum velocity */
  R w.    /* velocity bound 1 = lower, -1 = upper */
  R ao.   /* vertical acceleration of ownship */
  /** Constants **/
  R hp.   /* puck height */
  R rp.   /* puck radius */
  R a.    /* minimal vertical acceleration. Typically g/4 */

  R t.
  R ro.
  R ho.
End.

Problem.
  hp > 0 & rp > 0 & rv >= 0 & a > 0 &
  ( (w=-1 | w=1) &
      (
        \forall t \forall ro \forall ho
        ((0 <= t & t < maxz(w * (dhf - dhd)) / a &
          ro = rv * t & ho = (w * a) / 2 * t^2 + dhd * t) |
         (t >= maxz(w * (dhf - dhd)) / a &
          ro = rv * t & ho = dhf * t - w * (maxz(w * (dhf - dhd)))^2 / (2*a))
         -> (abs(r - ro) > rp | w * h < w * ho - hp))
      ) /* C(w,dhf) */
  )
  -> [
  {   {
      { ?true; ++
        {dhf :=*; {w:=-1; ++ w:=1;}
         ?(
        \forall t \forall ro \forall ho
        ((0 <= t & t < maxz(w * (dhf - dhd)) / a &
          ro = rv * t & ho = (w * a) / 2 * t^2 + dhd * t) |
         (t >= maxz(w * (dhf - dhd)) / a &
          ro = rv * t & ho = dhf * t - w * (maxz(w * (dhf - dhd)))^2 / (2*a))
         -> (abs(r - ro) > rp | w * h < w * ho - hp))
          ); /* C(w,dhf) */
        }}
        ao :=*;
      }
      {r' = -rv, dhd' = ao, h' = -dhd & (w * dhd >= w * dhf | w * ao >= a)}
   }*
  ] ((abs(r) > rp | abs(h) > hp) &
      (
        \forall t \forall ro \forall ho
        ((0 <= t & t < maxz(w * (dhf - dhd)) / a &
          ro = rv * t & ho = (w * a) / 2 * t^2 + dhd * t) |
         (t >= maxz(w * (dhf - dhd)) / a &
          ro = rv * t & ho = dhf * t - w * (maxz(w * (dhf - dhd)))^2 / (2*a))
         -> (abs(r - ro) > rp | w * h < w * ho - hp))
      ) /* C(w,dhf) */
      )
End.
